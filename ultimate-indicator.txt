// Â© Kaniugu
//@version=5
indicator("Ultimate Indicator", overlay = true)

// VALUE
var above = float(na)
var below = float(na)
var top = float(na)
var bottom = float(na)
var line center = na, var line resistance = na, var line support = na, var line subresistance = na, var line subsupport = na, var line stop = na
factor = input.float(1.2, "Reward Factor", minval = 1)
period = input.int(200, "Period", minval = 5)
extend = input.string("Extend", "Lines", options = ["Extend", "None"]) == "Extend" ? extend.right : extend.none
line.delete(center[1]), line.delete(resistance[1]), line.delete(support[1]), line.delete(subresistance[1]), line.delete(subsupport[1]), line.delete(stop[1])

// DIRECTION
up = open > close and open[1] < close[1]
down = open < close and open[1] > close[1]

// LEVEL
peak = up and high > above
trough = down and low < below

// THRESHOLD
if up
    above := high
if down
    below := low

// LABEL
if peak
    label.new(bar_index, na, str.format("{0,number,#.##}", (high - bottom) / high * 100), yloc = yloc.abovebar, style = label.style_none, textcolor = color.red)
    top := high
if trough
    label.new(bar_index, na, str.format("{0,number,#.##}", (top - low) / top * 100), yloc = yloc.belowbar, style = label.style_none, textcolor = color.blue)
    bottom := low

// LINEAR REGRESSION
x = 0.0, y = 0.0, x2 = 0.0, y2 = 0.0, xy = 0.0
index = period - 1
for i = 0 to index
    back = nz(close[i])
    x := x + i
    y := y + back
    x2 := x2 + (i * i)
    y2 := y2 + (back * back)
    xy := xy + (back * i)
sx = math.pow(x, 2.0)
sy = math.pow(y, 2.0)
pearson = (xy - (x * y) / period) / (math.sqrt(x2 - sx / period) * math.sqrt(y2 - sy / period))
xx = x * x
slope = x2 == xx ? 0.0 : (period * xy - x * y) / (period * x2 - xx)
linreg = (y - slope * x) / period
intercept = linreg + bar_index * slope
deviation = 0.0
for i = 0 to index
    deviation := deviation + math.pow(nz(close[i]) - (intercept - slope * bar_index[i]), 2.0)
deviation := math.sqrt(deviation / index) * 2
point = linreg + slope * index

// RISK & REWARD
price(value) => value > 10.0 ? str.format("{0,number,#.##}", value) : str.tostring(value)
atr = ta.atr(14)

// SIGNAL
if barstate.islast and (peak[1] or trough[1]) and (close > linreg + deviation or close < linreg - deviation)
    if close > linreg
        position = (high[2] > high[1] ? high[2] : high[1]) + atr, risk = position, reward = low - atr * factor
        stop := line.new(bar_index - 2, position, bar_index + 1, risk, color = color.red, width = 2)
        alert("Short\nTP " + price(reward) + "\nSL " + price(risk), alert.freq_once_per_bar)
    else
        position = (low[2] < low[1] ? low[2] : low[1]) - atr, risk = position, reward = high + atr * factor
        stop := line.new(bar_index - 2, position, bar_index + 1, risk, color = color.blue, width = 2)
        alert("Long\nTP " + price(reward) + "\nSL " + price(risk), alert.freq_once_per_bar)

// DRAW
center          := line.new(bar_index - period + 1, point                , bar_index, linreg                , xloc.bar_index, extend, pearson < 0 ? color.lime : color.aqua, line.style_dotted, 2)
resistance      := line.new(bar_index - period + 1, point + deviation    , bar_index, linreg + deviation    , xloc.bar_index, extend, color.red , line.style_dashed, 2)
support         := line.new(bar_index - period + 1, point - deviation    , bar_index, linreg - deviation    , xloc.bar_index, extend, color.blue, line.style_dashed, 2)
subresistance   := line.new(bar_index - period + 1, point + deviation / 2, bar_index, linreg + deviation / 2, xloc.bar_index, extend, color.red , line.style_dotted, 1)
subsupport      := line.new(bar_index - period + 1, point - deviation / 2, bar_index, linreg - deviation / 2, xloc.bar_index, extend, color.blue, line.style_dotted, 1)
ribbon = ta.ema(close, 24) > ta.ema(close, 8) ? color.aqua : color.lime
plotshape(peak, style = shape.xcross, color = color.red, location = location.abovebar, size = size.tiny, title = "Peak")
plotshape(trough, style = shape.diamond, color = color.blue, location = location.belowbar, size = size.tiny, title = "Trough")
plot(ta.ema(close,  8), color = color.new(ribbon, 40), title =  "EMA 8")
plot(ta.ema(close, 12), color = color.new(ribbon, 50), title = "EMA 12")
plot(ta.ema(close, 16), color = color.new(ribbon, 60), title = "EMA 16")
plot(ta.ema(close, 20), color = color.new(ribbon, 70), title = "EMA 20")
plot(ta.ema(close, 24), color = color.new(ribbon, 80), title = "EMA 24")
fill(plot((open + close) / 2, display = display.none), plot(ta.ema(close, 24), display = display.none), color = color.new(ribbon, 96))